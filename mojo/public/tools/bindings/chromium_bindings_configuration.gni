# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

_typemap_imports = [
  "//chrome/chrome_cleaner/mojom/typemaps/typemaps.gni",
  "//chrome/common/importer/typemaps.gni",
  "//chrome/typemaps.gni",
  "//chromecast/typemaps.gni",
  "//chromeos/typemaps.gni",
  "//chromeos/components/multidevice/mojom/typemaps.gni",
  "//chromeos/services/cros_healthd/public/mojom/typemaps.gni",
  "//chromeos/services/device_sync/public/mojom/typemaps.gni",
  "//chromeos/services/network_config/public/mojom/typemaps.gni",
  "//chromeos/services/secure_channel/public/mojom/typemaps.gni",
  "//content/browser/typemaps.gni",
  "//content/public/common/typemaps.gni",
]

_typemaps = []
foreach(typemap_import, _typemap_imports) {
  # Avoid reassignment error by assigning to empty scope first.
  _imported = {
  }
  _imported = read_file(typemap_import, "scope")
  _typemaps += _imported.typemaps
}

typemaps = []
foreach(typemap, _typemaps) {
  typemaps += [
    {
      filename = typemap
      config = read_file(typemap, "scope")
    },
  ]
}

component_macro_suffix = ""
